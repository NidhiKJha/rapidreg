apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.application'
apply from: './jacoco.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion '27.0.3'

    defaultConfig {
        def buildNumber = System.getenv("BUILD_NUMBER") == null ? '' : System.getenv("BUILD_NUMBER")
        if (buildNumber != '') {
            buildNumber = '0000'+ buildNumber
            buildNumber = buildNumber.reverse().take(4).reverse()
        }
        applicationId "org.unicef.rapidreg"
        minSdkVersion 17
        targetSdkVersion 19
        versionCode 2
        versionName "3.0.0.4"
    }
    signingConfigs {
        release {
            storeFile file("../rapidreg.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    project.ext {
                        appName = 'RapidReg'
                    }
                    def newName = outputFileName
                    newName = newName.replace("app-", "$project.ext.appName-v$defaultConfig.versionName")
                    newName = newName.replace("debug", "")

                    outputFileName = new File("./../../../../../build/", newName)
                }
            }
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    project.ext { appName = 'RapidReg' }
                    def newName = outputFileName
                    newName = newName.replace("app-", "$project.ext.appName-v$defaultConfig.versionName")
                    newName = newName.replace("release", "")
                    outputFileName = new File("./../../../../../build/", newName)
                }
            }
        }
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

def androidSupportVersion = '23.3.0'
def powerMockVersion = '1.6.5'
def dbflowVersion = '4.0.0-beta5'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${androidSupportVersion}"
    implementation "com.android.support:design:${androidSupportVersion}"
    implementation "com.android.support:recyclerview-v7:${androidSupportVersion}"
    implementation "com.android.support:support-v4:${androidSupportVersion}"
    implementation "com.android.support:cardview-v7:${androidSupportVersion}"

    implementation 'com.google.dagger:dagger:2.4'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.4'
    implementation 'com.jakewharton:butterknife:8.0.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.0.1'

    implementation 'io.reactivex:rxjava:1.1.5'
    implementation 'io.reactivex:rxandroid:1.2.0'

    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.hannesdorfmann.mosby:mvp:2.0.1'
    implementation 'com.hannesdorfmann.mosby:viewstate:2.0.1'
    implementation 'id.zelory:compressor:1.0.4'

    implementation 'com.hannesdorfmann.annotatedadapter:annotation:1.1.1'
    implementation 'com.hannesdorfmann.annotatedadapter:support-recyclerview:1.1.1'
    annotationProcessor 'com.hannesdorfmann.annotatedadapter:processor:1.1.1'

    implementation 'com.github.bumptech.glide:glide:3.7.0'

    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflowVersion}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflowVersion}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflowVersion}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:${dbflowVersion}"


    implementation 'org.mindrot:jbcrypt:0.4-atlassian-1'

    implementation 'me.relex:circleindicator:1.2.1@aar'

    implementation 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:1.6.1@aar'

    implementation 'com.facebook.stetho:stetho:1.3.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    implementation 'com.squareup.okhttp3:okhttp:3.3.1'

    testImplementation 'junit:junit:4.12',
            'org.robolectric:robolectric:3.1.1',
            'org.easymock:easymock:3.4',
            "org.powermock:powermock:${powerMockVersion}",
            "org.powermock:powermock-module-junit4:${powerMockVersion}",
            "org.powermock:powermock-api-mockito:${powerMockVersion}",
            "org.powermock:powermock-api-easymock:${powerMockVersion}"

    androidTestImplementation 'junit:junit:4.12',
            'org.robolectric:robolectric:3.1.1',
            "org.powermock:powermock-module-junit4:${powerMockVersion}",
            "org.powermock:powermock-api-mockito:${powerMockVersion}",
            'com.google.dagger:dagger:2.4'

    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.4'

    implementation 'de.hdodenhof:circleimageview:2.0.0'
    implementation 'org.robolectric:robolectric-annotations:3.1.1'
    implementation 'com.google.guava:guava:19.0'
}

